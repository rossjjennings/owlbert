?start: relation (DOUBLESLASH postfix)*
?relation: expression (rel_op expression)?
expression: term (add_op term)*
?term: factor (mul_op factor)*
?factor: unary_op? exponential
?exponential: factorial (exp_op factor)?
?factorial: value fact_op?
?value: NUMBER | CNAME | "(" expression ")" | function
function: funcname "(" expression ")"
?postfix: CNAME
?funcname: CNAME
rel_op: "=" -> equals
      | "<" -> less_than
      | ">" -> greater_than
      | ("<=" | "≤") -> less_than_or_equal
      | (">=" | "≥") -> greater_than_or_equal
      | ("!=" | "≠") -> not_equal
unary_op: "+" -> plus
        | "-" -> minus
add_op: "+" -> plus
      | "-" -> minus
mul_op: "*" -> times
      | "/" -> divided_by
exp_op: ("^"|"**") -> to_the
fact_op: "!" -> factorial
       | "!!" -> double_factorial

DOUBLESLASH: "//"
CNAME: ("_"|LETTER) ("_"|LETTER|DIGIT)*
LETTER: LATIN_LETTER | GREEK_LETTER | INFINITY
GREEK_LETTER: GREEK_LCASE_LETTER | GREEK_UCASE_LETTER
GREEK_LCASE_LETTER: "α".."ω"
GREEK_UCASE_LETTER: "A".."Ω"
INFINITY: "∞"

%import common.NUMBER
%import common.LETTER -> LATIN_LETTER
%import common.DIGIT
%ignore " "
